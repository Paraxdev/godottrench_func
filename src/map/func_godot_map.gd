@tool
@icon("res://addons/func_godot/icons/icon_slipgate3d.svg")
class_name FuncGodotMap extends Node3D
## Scene generator node that builds a .gtm map according to its [FuncGodotMapSettings].
##
## A scene generator node that parses a .gtm map. It uses a [FuncGodotMapSettings] 
## and the [FuncGodotFGDFile] contained within in order to determine what is built and how it is built.[br][br]
## If your map is not building correctly, double check your [member map_settings] to make sure you're using 
## the correct [FuncGodotMapSettings].

const _SIGNATURE: String = "[MAP]"

## Bitflag settings that control various aspects of the build process.
enum BuildFlags {
	UNWRAP_UV2 			= 1 << 0,	## Unwrap UV2s during geometry generation for lightmap baking.
	SHOW_PROFILE_INFO 	= 1 << 1,	## Print build step information during build process.
	DISABLE_SMOOTHING	= 1 << 2	## Force disable processing of vertex normal smooth shading.
}

## Emitted when the build process fails.
signal build_failed

## Emitted when the build process succesfully completes.
signal build_complete

@export_tool_button("Build Map","CollisionShape3D") var _build_func: Callable = build
@export_tool_button("Clear Map","Skeleton3D") var _clear_func: Callable = clear_children

@export_category("Map")
## Local path to the .gtm map to build a scene from.
@export_file("*.gtm") var local_map_file: String = ""

## Global path to the .gtm map to build a scene from. Overrides [member FuncGodotMap.local_map_file].
@export_global_file("*.gtm") var global_map_file: String = ""

## GodotTrench: rebuild automatically when the GodotTrench editor reports that this map was saved (live link).
@export var auto_rebuild_on_save: bool = true

# Map path used by code. Do it this way to support both global and local paths.
var _map_file_internal: String = ""

## Map settings resource that defines map build scale, textures location, entity definitions, and more.
@export var map_settings: FuncGodotMapSettings = load(ProjectSettings.get_setting("func_godot/default_map_settings", "res://addons/func_godot/func_godot_default_map_settings.tres"))

@export_category("Build")
## [enum BuildFlags] that can affect certain aspects of the build process.
@export_flags("Unwrap UV2:1", "Show Profiling Info:2", "Disable Smooth Shading:4") var build_flags: int = 0

## The hyperplane is an initial plane that all geometry faces are cut from, like a large sheet of marble before a sculptor begins chiseling. 
## The hyperplane size would need to be able to cover your map's potential total area.
## Smaller values can minimize floating point errors, reducing the effect of gaps between polygon seams.
## Measured in Godot units, not Quake units.
@export_range(256.0, 2048.0, 128.0) var hyperplane_size: float = 512.0

## Map build failure handler. Displays error message and emits [signal build_failed] signal.
func fail_build(reason: String, notify: bool = false) -> void:
	push_error(_SIGNATURE, " ", reason)
	if notify:
		build_failed.emit()

## Frees all children of the map node.[br]
## GodotTrench: except [GodotTrenchOverlay] nodes and nodes in the [code]godottrench_keep[/code] group, which keep their
## place and node instances. Any other user created node directly under the map is freed with the generated ones.
func clear_children() -> void:
	for child in get_children():
		if GodotTrenchOverlay.is_kept(child):
			continue
		remove_child(child)
		child.queue_free()
	if has_meta(GodotTrenchIO.CACHE_META):
		remove_meta(GodotTrenchIO.CACHE_META)
	if Engine.is_editor_hint():
		Engine.get_singleton(&"EditorInterface").mark_scene_as_unsaved()

## Checks if a map file for the build process is provided and can be found.
func verify() -> Error:
	# Prioritize global map file path for building at runtime
	_map_file_internal = global_map_file if global_map_file != "" else local_map_file
	
	if _map_file_internal.is_empty():
		fail_build("Cannot build empty map file.")
		return ERR_INVALID_PARAMETER
	
	# Retrieve real path if needed
	if _map_file_internal.begins_with("uid://"):
		var uid := ResourceUID.text_to_id(_map_file_internal)
		if not ResourceUID.has_id(uid):
			fail_build("Error: failed to retrieve path for UID (%s)" % _map_file_internal)
			return ERR_DOES_NOT_EXIST
		_map_file_internal = ResourceUID.get_id_path(uid)

	if _map_file_internal.get_extension().to_lower() != "gtm":
		fail_build("%s is not a .gtm map." % _map_file_internal)
		return ERR_FILE_UNRECOGNIZED

	if not FileAccess.file_exists(_map_file_internal):
		if not FileAccess.file_exists(_map_file_internal + ".import"):
			fail_build("Map file %s does not exist." % _map_file_internal)
			return ERR_DOES_NOT_EXIST
	
	return OK

## Builds the [member global_map_file]. If not set, builds the [member local_map_file].
## First cleans the map node of any children, then creates a [FuncGodotParser], [FuncGodotGeometryGenerator] 
## and [FuncGodotEntityAssembler] to parse and generate the map. 
func build() -> void:
	_build("")

## GodotTrench: builds the .gtm map from [param text] instead of its file, e.g. unsaved edits sent by the GodotTrench editor.
## The file path is still used to resolve prefabs.
func build_from_text(text: String) -> void:
	_build(text)

func _build(text: String) -> void:
	var time_elapsed: float = Time.get_ticks_msec()

	if build_flags & BuildFlags.SHOW_PROFILE_INFO:
		FuncGodotUtil.print_profile_info("Building...", _SIGNATURE)

	clear_children()

	var verify_err: Error = verify()
	if verify_err != OK:
		fail_build("Verification failed: %s. Aborting map build" % error_string(verify_err), true)
		return

	if not map_settings:
		push_warning("Map assembler does not have a map settings provided and will use default map settings.")
		load(ProjectSettings.get_setting("func_godot/default_map_settings", "res://addons/func_godot/func_godot_default_map_settings.tres"))

	# Parse and collect map data
	var parser := FuncGodotParser.new()
	if build_flags & BuildFlags.SHOW_PROFILE_INFO:
		print("\nPARSER")
		parser.declare_step.connect(FuncGodotUtil.print_profile_info.bind(parser._SIGNATURE))
	var parse_data: FuncGodotData.ParseData
	if text != "":
		parse_data = parser.parse_gtm(text, map_settings, _map_file_internal)
	else:
		parse_data = parser.parse_map_data(_map_file_internal, map_settings)
	# GodotTrench: lets a live session tell whether the scene still matches the map it was built from.
	if text != "" or Engine.is_editor_hint():
		set_meta(GodotTrenchBuild.SOURCE_HASH_META, text.hash() if text != "" else GodotTrenchGtmFile.content_id(_map_file_internal))
	
	if parse_data.entities.is_empty():
		return	# Already printed failure message in parser, just return here
	
	var entities: Array[FuncGodotData.EntityData] = parse_data.entities
	var groups: Array[FuncGodotData.GroupData] = parse_data.groups
	
	# Free up some memory now that we have the data
	parser = null
	
	# Retrieve geometry
	var generator := FuncGodotGeometryGenerator.new(map_settings, hyperplane_size)
	if build_flags & BuildFlags.SHOW_PROFILE_INFO:
		print("\nGEOMETRY GENERATOR")
		generator.declare_step.connect(FuncGodotUtil.print_profile_info.bind(generator._SIGNATURE))
	
	# Generate surface and shape data
	var generate_error := generator.build(build_flags, entities)
	if generate_error != OK:
		fail_build("Geometry generation failed: %s" % error_string(generate_error))
		return

	# Assemble entities and groups
	var assembler := FuncGodotEntityAssembler.new(map_settings)
	if build_flags & BuildFlags.SHOW_PROFILE_INFO:
		print("\nENTITY ASSEMBLER")
		assembler.declare_step.connect(FuncGodotUtil.print_profile_info.bind(assembler._SIGNATURE))
	assembler.build(self, entities, groups)

	# GodotTrench: heightmap terrains are built as their own nodes after the entities.
	GodotTrenchTerrain.build_all(self, parse_data.terrains, map_settings)
	# GodotTrench: scatter sets (trees, rocks, foliage) as MultiMesh and shared collision.
	GodotTrenchScatter.build_all(self, parse_data.scatters, map_settings)
	# GodotTrench: sky, fog and sun from worldspawn keys, the same values the editor's lit preview uses.
	GodotTrenchEnvironment.build(self, entities[0].properties)
	# GodotTrench: the chunk streamer goes in last, it groups everything built above.
	var streamer := GodotTrenchStreamer.build(self, entities[0].properties, map_settings)
	if streamer:
		streamer.rebuild()

	# GodotTrench: overlays and their anchors catch up with the rebuilt entities.
	GodotTrenchOverlay.notify_built(self)

	time_elapsed = Time.get_ticks_msec() - time_elapsed

	if build_flags & BuildFlags.SHOW_PROFILE_INFO:
		print("\nCompleted in %s seconds" % (time_elapsed / 1000.0))

	if build_flags & BuildFlags.SHOW_PROFILE_INFO:
		print("")
		FuncGodotUtil.print_profile_info("Build complete", _SIGNATURE)
	build_complete.emit()
